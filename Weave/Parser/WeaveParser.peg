@namespace Weave.Parser
@classname WeaveParser
@using System.Linq
@using Weave.Expressions

template <Template>
    = settings:setting* EOL? elements:element* EOF {
        new Template(settings, settingsEnd, elements)
    }

setting <KeyValuePair<SourceSpan, SourceSpan>>
    = "@" key:identifier (!EOL WS)+ value:("" (!EOL .)+) (EOL / !.) {
        new KeyValuePair<SourceSpan, SourceSpan>(
            new SourceSpan(key, keyStart, keyEnd),
            new SourceSpan(value, valueStart, valueEnd))
    }

element <Element>
    = tag
    / text

tag <Element>
    = ifTag
    / eachTag
    / "{{:" WS? expr:expression "}}" { new EchoTag(expr, encoded: true) }
    / "{{=" WS? expr:expression "}}" { new EchoTag(expr, encoded: false) }
    / "{{" &WS expr:expression "}}" { new CodeElement(expr) }

ifTag <IfTag>
    = if:ifBody elseIfs:elifBody* else:elseBody? endIf {
        new IfTag(new[] { @if }.Concat(elseIfs).Concat(@else))
    }

ifBody <Branch>
    = "{{if" &WS expr:expression "}}" body:element* { new Branch(expr, body) }

elifBody <Branch>
    = "{{elif" &WS expr:expression "}}" body:element* { new Branch(expr, body) }

elseBody <Branch>
    = "{{else}}" body:element* { new Branch(null, body) }

endIf
    = "{{/if}}"

eachTag <EachTag>
    = "{{each" &WS expr:expression "}}" body:element* none:noneBody? endEach {
        new EachTag(expr, body, none.SingleOrDefault())
    }

noneBody <IList<Element>>
    = "{{none}}" body:element* { body }

endEach
    = "{{/each}}"

text <TextElement>
    = value:("" (!"{{" .)+) { new TextElement(value) }

expression <SourceSpan>
    = text:("" (!"}}" .)+) { new SourceSpan(text, textStart, textEnd) }

identifier
    = !digit (letter / digit / "_" / "$")+

digit
  = [0-9]

letter
  = lowerCaseLetter
  / upperCaseLetter

lowerCaseLetter
  = [a-z]

upperCaseLetter
  = [A-Z]

WS
    = [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]
    / EOL

EOL
    = "\n"
    / "\r\n"
    / "\r"
    / "\u2028"
    / "\u2029"

EOF
  = !.
  / unexpected:. #ERROR{ string.Format("Unexpected character '{0}'.", unexpected) }
